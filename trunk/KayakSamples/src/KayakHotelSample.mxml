<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
  			    xmlns:controls="net.infoaccelerator.controls.*"
				width="100%" height="100%" 
				layout="absolute" 
				creationComplete="onInitView(event)">
	<mx:Script>
		<![CDATA[
			import com.mapquest.tilemap.DefaultRolloverWindow;
			import com.mapquest.tilemap.DefaultInfoWindow;
			import com.mapquest.tilemap.pois.MapIcon;
			import mx.events.ValidationResultEvent;
			import mx.validators.PhoneNumberValidator;
			import com.mapquest.tilemap.pois.PoiEvent;
			import net.infoaccelerator.travel.kayak.vo.HotelSearchResult;
			import com.mapquest.tilemap.pois.PoiCollection;
			import net.infoaccelerator.travel.kayak.vo.Hotel;
			import net.infoaccelerator.travel.kayak.events.AmbiguousLocationEvent;
			import net.infoaccelerator.travel.kayak.events.SearchFailureEvent;
			import com.mapquest.tilemap.pois.Poi;
			import com.mapquest.PointLL;
			import mx.collections.ArrayCollection;
			import net.infoaccelerator.travel.kayak.events.SearchCompleteEvent;
			
			import mx.controls.Alert;
			import mx.managers.CursorManager;
			import mx.formatters.CurrencyFormatter;
			import mx.formatters.PhoneFormatter;
			
			import net.infoaccelerator.travel.kayak.searches.KayakHotelSearch;	
			import net.infoaccelerator.assets.SearchAssets;
			
			import com.mapquest.tilemap.controls.LargeZoomControl;
  			import com.mapquest.tilemap.controls.ViewControl;				
					
			private var _key:String = "esm3gPbxTNuWHmfrb_7Azw";
			[Bindable] private var hotelSearch:KayakHotelSearch = new KayakHotelSearch();
			
			[Bindable] private var _mapquestKey:String = "mjtd%7Clu6y2l02ng%2Crw%3Do5-001a5";		
		    private var _oldPoi:Poi;
		    private var _currentPoi:Poi;
		    private var _mapIcon:MapIcon = new MapIcon();			
			private var _info:DefaultInfoWindow;
			private var _roll:DefaultRolloverWindow;
			private var _lrgZoomControl:LargeZoomControl = new LargeZoomControl;
			private var _viewControl:ViewControl = new ViewControl();
			
			public function onInitView(e:Event):void{
				
				//add map controls
				_lrgZoomControl.setBackgroundColor(0xA7C58E);
				_lrgZoomControl.setBorderColor(0xffffff);
				_lrgZoomControl.setBorderAlpha(1);
				_lrgZoomControl.setBackgroundEllipseWidth(0);				
				_lrgZoomControl.setBackgroundEllipseHeight(0);
				_lrgZoomControl.setBackgroundAlpha(.9);
				
				this.map.addControl(_lrgZoomControl);
	      		this.map.addControl(_viewControl);
				
				_mapIcon.setImage(new SearchAssets.HOTELICON());
				//reference the maps info window
				_info = map.getInfoWindow() as DefaultInfoWindow;
				_info.setBackgroundColor(0xA7C58E);
				_info.setBorderColor(0xffffff);
				_info.setBorderAlpha(1);
				_info.setBackgroundEllipseWidth(0);				
				_info.setBackgroundEllipseHeight(0);
				_info.setBackgroundAlpha(.9);
				
				//reference the maps rollover window
				_roll = map.getRolloverWindow() as DefaultRolloverWindow;
				_roll.setBackgroundColor(0xA7C58E);
				_roll.setBorderColor(0xffffff);
				_roll.setBorderAlpha(1);
				_roll.setBackgroundEllipseWidth(0);				
				_roll.setBackgroundEllipseHeight(0);
				_roll.setBackgroundAlpha(.9);
				
				//center the main panel
				kayakPanel.x = (this.width - kayakPanel.width)/2;
				kayakPanel.y = (this.height - kayakPanel.height)/2;
				
				hotelSearch.devKey 			= _key;
				hotelSearch.checkin_date 	= new Date();
				hotelSearch.checkout_date 	= new Date();
				hotelSearch.city 			= "";
				hotelSearch.filterMode 		= "normal";
				hotelSearch.guests 			= 1;
				hotelSearch.rooms 			= 1;
				hotelSearch.sortDirection 	= "up";
				hotelSearch.sortKey 		= "price";
				hotelSearch.addEventListener("searchComplete",onSearchComplete);
				hotelSearch.addEventListener("searchFailure",onSearchFailure);
				hotelSearch.addEventListener("ambiguousLoction",onAmbiguousLocation);
			
			}
			
			private function onFilterModeChange(e:Event):void{
				if(filterMode.value == "stars"){
					this.starLabel.enabled 	= true;
					this.stars.enabled 		= true;
				}
				else{
					this.starLabel.enabled 	= false;
					this.stars.enabled 		= false;
				}
			}
			
			private function onSearchBtnClick(e:Event):void{
				if(!hotelSearch.loading){
					hotelSearch.checkin_date = this.checkin_date.selectedDate;
					hotelSearch.checkout_date = this.checkout_date.selectedDate;
					hotelSearch.city = this.city.text;
					hotelSearch.filterMode = this.filterMode.selectedItem as String;
					hotelSearch.filterStars = this.stars.value;
					hotelSearch.guests = this.guests.value as Number;
					hotelSearch.rooms = this.rooms.value as Number;					
					hotelSearch.hotelResults = new HotelSearchResult();
					map.removeAllPois();
										
					CursorManager.setBusyCursor();
					hotelSearch.doSearch();
				}
					else
						Alert.show("Search In Progress.  Please Wait, then try again","Search In Progress");
			}
			
			
			private function onSearchComplete(e:SearchCompleteEvent):void{
				var hotels:ArrayCollection = hotelSearch.hotelResults.hotels;
				var hotelLen:int 	= hotels.length;
							
				for(var i:int=0;i<hotelLen;i++){
					if(i==0){
						var newCenter:PointLL = new PointLL(hotels.getItemAt(i).lat,hotels.getItemAt(i).lon);
						this.map.setCenter(newCenter,7);
						var poi:Poi = new Poi(newCenter);
						poi.setInfoTitle(hotels.getItemAt(i).name);
						poi.setInfoContent("Stars: " + hotels.getItemAt(i).stars.toString() + "\nPhone: " + hotels.getItemAt(i).phone + "\nLow Price: " + formatPrice(hotels.getItemAt(i).priceLow) + "\nHigh Price: " + formatPrice(hotels.getItemAt(i).priceHigh));
						poi.setKey(hotels.getItemAt(i).id);
						poi.addEventListener(PoiEvent.INFOWINDOW_OPEN,onPOIChosen);
						this.map.addPoi(poi);
					}
					else{
						var newPoint:PointLL = new PointLL(hotels.getItemAt(i).lat,hotels.getItemAt(i).lon);
						var poi:Poi = new Poi(newPoint);
						poi.setInfoTitle(hotels.getItemAt(i).name);
						poi.setInfoContent("Stars: " + hotels.getItemAt(i).stars.toString() + "\nPhone: " + hotels.getItemAt(i).phone + "\nLow Price: " + formatPrice(hotels.getItemAt(i).priceLow) + "\nHigh Price: " + formatPrice(hotels.getItemAt(i).priceHigh));
						poi.setKey(hotels.getItemAt(i).id);
						poi.addEventListener(PoiEvent.INFOWINDOW_OPEN,onPOIChosen);
						this.map.addPoi(poi);
					}
				}
				CursorManager.removeBusyCursor();
			}
			
			private function onSearchFailure(e:SearchFailureEvent):void{
				CursorManager.removeBusyCursor();
				var errorString:String = "";
				for(var i:int=0;i<e.errors.length;i++){
					errorString += e.errors.getItemAt(i) + "\n";
				}
				Alert.show(errorString,"Search Error");
			}
			
			private function onAmbiguousLocation(e:AmbiguousLocationEvent):void{
				CursorManager.removeBusyCursor();
				var locationString:String = "Location Suggestions: \n";
				for(var i:int=0;i<e.suggestions.length;i++){
					locationString += e.suggestions.getItemAt(i) + "\n";
				}
				Alert.show(locationString,"Ambiguous Location");
			}
			
			private function onHotelGridClick():void{
				var currentHotel:Hotel = hotelGrid.selectedItem as Hotel;
				var poiArray:PoiCollection = this.map.getPois();
				var poiLen:int = poiArray.length;
				
				if (_currentPoi != null)
				{
					this.map.removePoi(_currentPoi);
					this.map.addPoi(_oldPoi);
				}
					
				for(var i:int=0;i < (poiLen - 1);i++){
					_currentPoi = poiArray.get(i);
					if(currentHotel != null && (_currentPoi.getKey() == currentHotel.id.toString())){
						var newCenter:PointLL = new PointLL(new Number(currentHotel.lat), new Number(currentHotel.lon));
						_currentPoi.setIcon(_mapIcon);
						_oldPoi = poiArray.get(i);
						this.map.setCenter(newCenter,11);
						this.map.removePoi(_currentPoi);
						this.map.addPoi(_currentPoi);
						break;
					}
				}
				
				//slide the embed panel out if it's hidden
				showPanel();
				
			}
			
			private function onPOIChosen(e:PoiEvent):void{
				var poi:Poi = e.poi;
				var hotels:ArrayCollection = hotelGrid.dataProvider as ArrayCollection;
				var hotelCnt:int = hotels.length;
				for(var i:int=0;i<hotelCnt;i++){
					var cHotel:Hotel = hotels.getItemAt(i) as Hotel;
					if(new Number(poi.getKey()) == cHotel.id){
						hotelGrid.selectedIndex = i;
						break;
					}
				}
			}
			
			private function formatPrice(raw:Number):String{
				var formatter:CurrencyFormatter = new CurrencyFormatter();
				formatter.currencySymbol = "$";
				formatter.alignSymbol = "left";
				formatter.decimalSeparatorFrom = ".";
				formatter.decimalSeparatorTo = ".";
				return formatter.format(raw);
			}
			
			private function formatPhoneNumber(raw:String):String{
				var formatter:PhoneFormatter = new PhoneFormatter();
				var validator:PhoneNumberValidator = new PhoneNumberValidator();
				formatter.areaCodeFormat = "###";
				formatter.formatString = "###-###-####";
				formatter.validPatternChars = "#-() /";
				
				validator.property="text";
				validator.allowedFormatChars = "";
				validator.source = raw;
				
				var vResult:ValidationResultEvent = validator.validate();
				if (vResult.type==ValidationResultEvent.VALID)
					return formatter.format(raw);
				else
					return "";
				
			}
		  
		   	private function showPanel():void {
           		if (embedPanel.y != 343)
           		{     
	            	panelMove.end();
	                panelMove.yTo = 343; 
	                panelMove.play();
	            } 
            }			
			
			private function hidePanel():void {
 
            	panelMove.end();
                panelMove.yTo = 487; 
                panelMove.play();

            }	
			
			public function goToURL(urlStr:String):void {
       
		    	var webPageURL:URLRequest = new URLRequest( urlStr );
		       
		        navigateToURL(webPageURL, '_blank')
		       
		       
		    }//end function goToURL    
			
		]]>
	</mx:Script>	
	
	<mx:Style source="assets/css/stylesheet.css" />
		
	<mx:Move id="panelMove" target="{embedPanel}"/>	
		
	<mx:Panel width="90%" layout="absolute" id="kayakPanel" title="Ribbit, Kayak, MapQuest Hotel Search Mashup" horizontalScrollPolicy="off" verticalScrollPolicy="off" height="568">
				
		<mx:Form x="0" y="0" width="447" height="488" defaultButton="{searchButton}">				

			<mx:Label id="kayakHeader" text="Kayak Hotel Search" />
			<mx:FormItem id="citLbl" label="City:">
				<mx:TextInput id="city" enabled="true" cornerRadius="4" text="{hotelSearch.city}" toolTip="City, State (Atlanta, GA)"/>
			</mx:FormItem>
			<mx:FormItem id="inLabel" label="Check In:">
				<mx:DateField id="checkin_date" showToday="true"/>
			</mx:FormItem>
			<mx:FormItem id="outLabel" label="Check Out:">
				<mx:DateField id="checkout_date" showToday="true"/>
			</mx:FormItem>												
			<mx:FormItem id="guestLabel" label="Number of Guests:">
				<controls:AdvComboBox width="52" id="guests" enabled="true">
					<controls:dataProvider>
						<mx:ArrayCollection>
							<mx:Object label="1" data="1"/>
							<mx:Object label="2" data="2"/>
							<mx:Object label="3" data="3"/>
							<mx:Object label="4" data="4"/>
							<mx:Object label="5" data="5"/>
							<mx:Object label="6" data="6"/>
						</mx:ArrayCollection>
					</controls:dataProvider>
				</controls:AdvComboBox>
			</mx:FormItem>
			<mx:FormItem id="roomLabel" label="Number of Rooms:">
				<controls:AdvComboBox width="52" id="rooms" enabled="true">
					<controls:dataProvider>
						<mx:ArrayCollection>
							<mx:Object label="1" data="1"/>
							<mx:Object label="2" data="2"/>
							<mx:Object label="3" data="3"/>
						</mx:ArrayCollection>
					</controls:dataProvider>
				</controls:AdvComboBox>
			</mx:FormItem>
			
			<mx:FormItem id="filterLabel" label="Filter Mode:" width="226">
				<controls:AdvComboBox width="100" id="filterMode" enabled="true" change="onFilterModeChange(event)">
					<controls:dataProvider>
						<mx:ArrayCollection>
							<mx:Object label="normal" data="normal"/>
							<mx:Object label="stars" data="stars"/>
						</mx:ArrayCollection>
					</controls:dataProvider>
				</controls:AdvComboBox>
			</mx:FormItem>
			<mx:FormItem id="starLabel" label="Stars:" enabled="false">				
				<mx:HSlider id="stars" allowTrackClick="true" minimum="1" maximum="5" snapInterval="1" enabled="false"/>			
			</mx:FormItem>
			<mx:FormItem width="409" height="25">
				<mx:Button label="Start Hotel Search" id="searchButton" click="onSearchBtnClick(event)"/>	
			</mx:FormItem>
			<mx:Label id="hotelResultsHeader" text="Hotels Found: {hotelSearch.hotelResults.count}"/>
			<controls:hotelGrid id="hotelGrid" hotels="{hotelSearch.hotelResults.hotels}" change="onHotelGridClick();" xmlns:controls="net.infoaccelerator.controls.*" height="178" width="409"/>		
		</mx:Form>
		<mx:VBox x="455" y="0" width="90%">	
			<mq:TilemapComponent id="map" key="{this._mapquestKey}" zoom="3" width="100%" height="465" xmlns:mq="com.mapquest.tilemap.*" />
		</mx:VBox>
		 
		<controls:AdvPanel id="embedPanel" styleName="embedPanel" layout="absolute" x="455" y="487" backgroundAlpha=".7" backgroundColor="#ffffff" width="90%" headerHeight="16" horizontalScrollPolicy="off" verticalScrollPolicy="off" cancelClickEvent="hidePanel();">
			<controls:RibbitPane id="hotelCallPane" panelTitle="Call Hotel" numberToCall="{hotelGrid.selectedItem.phone}" width="305" height="117" xmlns:controls="net.infoaccelerator.controls.*" x="-18" y="-3"/>
			<mx:Form id="hotelDetailForm" y="-2" x="248">					
				<mx:FormItem label="Hotel:" id="hotelName">
					<mx:Label id="hotelLabel" truncateToFit="true" text="{hotelGrid.selectedItem.name}" />
				</mx:FormItem>
				<mx:FormItem label="Address:" id="addressLabel">
					<mx:Label text="{hotelGrid.selectedItem.address} {hotelGrid.selectedItem.city} {hotelGrid.selectedItem.region}"/>
				</mx:FormItem>
				<mx:FormItem label="Stars:" id="starsLabel">
					<mx:Label text="{hotelGrid.selectedItem.stars}"/>
				</mx:FormItem>
				<mx:FormItem label="Price Range:" id="rangeLabel">
					<mx:Label text="{formatPrice(hotelGrid.selectedItem.priceLow)} to {formatPrice(hotelGrid.selectedItem.priceHigh)}"/>
				</mx:FormItem>
			</mx:Form>
		</controls:AdvPanel>
		
		<mx:ControlBar width="100%">
			<mx:Spacer width="100%"/>
			<mx:Label text="Powered By: "/>
			<mx:Button id="btnRibbit" toolTip="Click to go to Ribbit Developer Site." upSkin="{SearchAssets.RIBBIT}" overSkin="{SearchAssets.RIBBIT_OVER}" downSkin="{SearchAssets.RIBBIT}" click="goToURL('http://developer.ribbit.com')" />
			<mx:Button id="btnKayak" toolTip="Click to go to Kayak Developer Site." upSkin="{SearchAssets.KAYAK}" overSkin="{SearchAssets.KAYAK_OVER}" downSkin="{SearchAssets.KAYAK}" click="goToURL('http://www.kayak.com/labs/')" />
			<mx:Button id="btnMapQuest" toolTip="Click to go to MapQuest Developer Network." upSkin="{SearchAssets.MAPQUEST}" overSkin="{SearchAssets.MAPQUEST_OVER}" downSkin="{SearchAssets.MAPQUEST}" click="goToURL('http://developer.mapquest.com/')" />
        </mx:ControlBar>
	</mx:Panel>
	
</mx:Application>
